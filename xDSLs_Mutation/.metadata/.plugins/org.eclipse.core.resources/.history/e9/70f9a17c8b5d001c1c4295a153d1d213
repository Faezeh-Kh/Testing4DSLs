generate 20 mutants
in "data/out/"
from "data/model/"
metamodel "/MiniJavaMutation/data/model/XMiniJava.ecore"

with blocks {
	cs "Changes the symbol of symbol ref with another symboy of the same type"{
		symbol1 = select one Symbol
		symbol2 = select one Symbol where {self <> symbol1 and typeRef = symbol1->typeRef}
		modify one SymbolRef where {symbol = symbol1} with {symbol = symbol2}
	}
	apr "Argument propagation for return statement" {
		stmt = select one Return where {expression is typed MethodCall}
		method = select one MethodCall in stmt->expression
		param = select one Expression in method->args
		modify stmt with {expression = param}
	}
	apar "Argument propagation And Expression rightOperand" {
		exp0 = select one And where {right is typed MethodCall}
		method = select one MethodCall in exp0->right
		param = select one Expression in method->args
		modify exp0 with {right = param}
	}
	apal "Argument propagation And Expression leftOperand" {
		exp0 = select one And where {left is typed MethodCall}
		method = select one MethodCall in exp0->left
		param = select one Expression in method->args
		modify exp0 with {left = param}
	}

	cbct "Changes the value of boolean constant: true"{
		modify one BoolConstant where {value = 'false'} with {value = 'true'}
	}
	cbcf "Changes the value of boolean constant: false"{
		modify one BoolConstant where {value = 'true'} with {value = 'false'}
	}
	csc "Changes the value of string constant"{
		modify one StringConstant with {value = random-string(1, 4)}
	}
	cic "Changes the value of integer constant"{
		modify one IntConstant with {value = random-int}
	}
}