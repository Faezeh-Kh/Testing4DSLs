generate 20 mutants
in "data/out/"
from "data/model/"
metamodel "/MiniJavaMutation/data/model/XMiniJava.ecore"

with blocks {
	cs "Changes the symbol of symbol ref with another symboy of the same type"{
		symbol1 = select one Symbol
		symbol2 = select one Symbol where {self <> symbol1 and typeRef = symbol1->typeRef}
		modify one SymbolRef where {symbol = symbol1} with {symbol = symbol2}
	}
	apmrt "Argument propagation" {
		stmt = select one Return where {expression is typed MethodCall}
		method = select one MethodCall in stmt->expression
		param = select one Expression in method->args
		modify stmt with {expression = param}
	}
	apmir "Argument propagation InfixExpression rightOperand" {
		exp0 = select one InfixExpression where {rightOperand is typed MethodInvocation}
		method = select one MethodInvocation in exp0->rightOperand
		param = select one SingleVariableAccess in method->arguments
		modify exp0 with {rightOperand = param}
	}
	apmil "Argument propagation InfixExpression leftOperand" {
		exp1 = select one InfixExpression where {leftOperand is typed MethodInvocation}
		method = select one MethodInvocation in exp1->leftOperand
		param = select one SingleVariableAccess in method->arguments
		modify exp1 with {leftOperand = param}
	}
	apmas "Argument propagation Assignment" {
		a = select one Assignment where {rightHandSide is typed MethodInvocation}
		method = select one MethodInvocation in a->rightHandSide
		param = select one SingleVariableAccess in method->arguments
		modify a with {rightHandSide = param}
	}
	cbct "Changes the value of boolean constant: true"{
		modify one BoolConstant where {value = 'false'} with {value = 'true'}
	}
	cbcf "Changes the value of boolean constant: false"{
		modify one BoolConstant where {value = 'true'} with {value = 'false'}
	}
	csc "Changes the value of string constant"{
		modify one StringConstant with {value = random-string(1, 4)}
	}
	cic "Changes the value of integer constant"{
		modify one IntConstant with {value = random-int}
	}
}